// launch pdftex -interaction=nonstopmode -src-specials "&preamble" "\def\incrcompilation{} \input slides-prg06 "


// -*- ASCII:EDT -*-
// TeX: PDFTeX ...

  Requires(20060601); // Requires this build of WinEdt to work properly

  SetFocus('PDFTeX ...');
  IfOK('Exit;');  // Already Running: focus it and exit...

  SaveRegisters(1111111111); // Clean Macro writing...

  SetOK(1);  // Just in case ...
  SetErrorFlag(0);

  // Display a Dialog for extra PDFTeX switches:
  IfStr("%$('PDFTeX-GUI');","1","=",>
    !|GetString("Switches: ","PDFTeX Switches","%$('PDFTeX--Switches');");>
      IfOK(!`Assign('PDFTeX--Switches','%!?');`,!`Exit;`);|,>
    !|Release('PDFTeX--Switches');|);

  SetOK(1);  // Just in case ...
  SetErrorFlag(0);

// Close the current Document in Acrobat (Reader) using DDE
// Avoid the error message because Acrobat locks the ".pdf" file for writing...
  Exe('%b\Exec\Acrobat CloseDoc.edt');

  SetOK(1);  // Just in case ...
  SetErrorFlag(0);
  CloseFile("%b\_Run.log",1);

  IfStr("%$('PDFTeX-Wait');","1","=",!|StartWorking("PDFTeX %N%T");|);

  // Delete the working output files...
  IfFileExists("%b\_Err.log",!"DeleteFile('%b\_Err.log')");
  IfFileExists("%b\_Out.log",!"DeleteFile('%b\_Out.log')");
  IfFileExists("%b\_Run.log",!"DeleteFile('%b\_Run.log')");
  IfFileExists("%P\%N.pdf",!"DeleteFile('%P\%N.pdf')");
  IfFileExists("%O\%N.log",!"DeleteFile('%O\%N.log')");
  SetLogFilename(!"%O\%N.log");
  UpdateStatus;

  LetReg(0, !"0");
  LetReg(1, !"0");
  LetReg(2, !"0");
  IfStr("%$('PDFTeX-Wait');",     "1","=",!|LetRegNum(0, "%!0+00100");|);
  IfStr("%$('PDFTeX-Exec_Mode');","2","=",!|LetRegNum(0, "%!0+01000");|);
  IfStr("%$('PDFTeX-Exec_Mode');","1","=",!|LetRegNum(0, "%!0+10000");|);
  IfStr("%$('PDFTeX-STD_IO');",   "1","=",!|LetRegNum(0, "%!0+00010");|);
  IfStr("%$('PDFTeX-Pause_Active_Only');","1","=",!|LetRegNum(1,"%!1+100");|);
  IfStr("%$('PDFTeX-Pause_Error');",      "1","=",!|LetRegNum(1,"%!1+010");|);
  IfStr("%$('PDFTeX-Pause_OK');",         "1","=",!|LetRegNum(1,"%!1+001");|);
  IfStr("%$('PDFTeX-High_Priority');","1",!"=",!|LetReg(2,'1');|);
  LetReg(4);
  IfStr("%$('PDFTeX-No_Interaction');","1",!"=",!|LetReg(4,'-interaction nonstopmode');|);
  IfStr("%$('PDFTeX-C-style-errors');","1",!"=",!|LetReg(4,'-file-line-error-style');|);
  // Output Directory?
  LetReg(5);
  IfStr('%O','%P','<>',>
    !|LetReg(5, !'-aux-directory="%O"');>
      IfFileExists('%O\',!'Relax;',!'CreateFolders("%O",1);');|);
//      "&preamble" "\def\incrcompilation{} \input slides-prg06 "
  LetReg(3, "%$('PDFTeX-Executable'); %$('PDFTeX-Switches'); %!4 %!5 %$('PDFTeX--Switches'); &preamble \def\incrcompilation{} \input %$('PDFTeX-Parameters'); ");
  Do("LetReg(3, `%!3`);"); // Expand Command Line in Register %!3
  // Prompt("%!3"); // For Debugging purposes

  // *** Execute it!!!
  SetOK(1); // Just in case
  WinExe('','%!3','%P','PDFTeX ...', %!0, %!2,>
         '', '%b\_Out.log', '%b\_Err.log',%!1);

  IfOK(!'Relax;',!'StopWorking;>
    Prompt("Error starting PDFTeX!%\>
 See WinEdt''s  TeX Configuration Wizard%\>
 for detailed diagnosis of your TeX system.");>
      UpdateStatus;RestoreRegisters(1111111111);ErrorLogUpdate;Exit;');

  UpdateStatus;

  IfStr("%$('PDFTeX-Wait');","1","=",!|Relax;|,!|RestoreRegisters(1111111111);Exit;|);

  ErrorLogUpdate;

  IfStr("%$('PDFTeX-STD_IO');","1","=",>
  !|>
    // Create a log file!!!<>
    OpenOutput('%b\_Run.log');  // Open Run's Log File<>
   |);

    // Insert the general information ...
    GetDate(9, 'dddd, mmmm d, yyyy "at" hh:nn'); // Date Stamp
    WrL('Output captured by WinEdt on %!9');
    WrL(|>
------------------------------------------------------------------%\>
%\>
WinEdt has successfully executed %$('TeX System'); PDFTeX accessory.%\>
%\>
If PDFTeX encountered and reported errors the resulting dvi file%\>
may not appear properly displayed or in some cases may not be%\>
created at all. For your convenience WinEdt captured output and%\>
error files produced by the PDFTeX accessory.%\>
%\>
If there are problems (eg. if WinEdt displayed this file) read%\>
this output carefully, fix any offending errors in your documents%\>
and recompile your sources. If you are having troubles locating%\>
and fixing errors please consult the extensive documentation%\>
pertaining to PDFTeX in %$('TeX System'); Doc folder.%\>
%\>
  Hint: WinEdt is almost certainly irrelevant to this situation!%\>
%\>
WinEdt may be able to assist you with locating the most common%\>
PDFTeX's errors and warnings based on the information in TeX's log%\>
file. If any such errors were detected you'll see the Error Log%\>
panel displayed at the bottom of your screen.%\>
%\>
  Hint: use the Ctrl+E shortcut to "walk" through Errors and%\>
  Warnings in your documents (as reported by TeX)...%\>
%\>
------------------------------------------------------------------%\>
    |);
    IfFileExists('%b\_Err.log',!'ReadFile("%b\_Err.log", 9);',!'LetReg(9,"");');
    IfStr("%!9","","=",>
      !"Relax;>
        WrL('No errors were reported during the execution of PDFTeX.');>
        WrL('*** Error File is empty!');>
        IfFileExists('%b\_Out.log',!'ReadFile(""%b\_Out.log"", 9);',!'LetReg(9,"""");');>
        IfStr('%!9','','=',>
          !|WrL;>
            WrL('*** External application did not produce any console output!');>
            Relax;|,>
          !|WrL;>
            WrL('*** WinEdt captured the following console output:');>
            WrL;>
            WrL('%!9');>
            Relax;|);",>
      !"Relax;>
        WrL('Errors were reported during the execution of PDFTeX.');>
        WrL('*** WinEdt captured the following error file:');>
        WrL;>
        WrL('%!9');>
        IfFileExists('%b\_Out.log',!'ReadFile(""%b\_Out.log"", 9);',!'LetReg(9,"""");');>
        IfStr('%!9','','=',>
          !|WrL;>
            WrL('*** External application did not produce any console output!');>
            Relax;|,>
          !|WrL;>
            WrL('*** WinEdt captured the following console output:');>
            WrL;>
            WrL('%!9');>
            Relax;|);>
        Relax;");
    CloseOutput;    // Close Log file
    IfFileExists("%b\_Err.log",!"DeleteFile('%b\_Err.log')");
    IfFileExists("%b\_Out.log",!"DeleteFile('%b\_Out.log')");
    RestoreRegisters(1111111111);
    StopWorking;
    UpdateStatus;   // Update WinEdt's Menu etc...

    // Only "real / serious" Errors (not Warnings)
    IfStr("%$('PDFTeX-Errors');","1","=",>
     !|>
        IfLogErrors(!"FindError(0,1,0,4,2);Exit;",>
          !`IfStr("%$('PDFTeX-Warnings');","1","<>",!"ShowErrorLog(0);");`,0,4,2);>
      |);
    // All Enabled Errors and Warnings
    IfStr("%$('PDFTeX-Warnings');","1","=",>
     !|>
        IfLogErrors(!"ShowErrorLog(1);",!"ShowErrorLog(0);",0,4,0);>
      |);
    // Exit Code: Errors (non-zero return)?
    IfStr("%$('PDFTeX-ExitCode');","1","=",>
     !|>
        GetExitCode(0);>
        IfNum('%!0','0','=',!'Relax;',>
          !'Prompt("PDFTeX returned nonzero Exit Code (%!0) indicating that some problems might have occurred during the compilation...");');>
      |);

//    IfFileExists("%P\%N.pdf",>
//      !`IfStr("%$('PDFTeX-Viewer');","1","=",>
//          !|Exe('%b\Exec\Acrobat OpenDoc.edt');|);>
//       `,>
   IfFileExists("%P\%N.dvi",>
     !`IfStr("%$('LaTeX-Viewer');","1","=",>
         !``>
            IfStr("%$('LaTeX-View_Search');","1","=",>
             !*Run(|%$('DVIView'); -1 -s "%!l %p\%n%t" "%P\%N.dvi"|,'%P');*,>
             !*Run(|%$('DVIView'); -1 "%P\%N.dvi"|,'%P');*);>
          ``);`,>
// Something is wrong with your TeX system or your document>
// Nothing WinEdt can do for you at this stage...>
      !`Prompt("PDFTeX failed to create a pdf file.%\>
 For possible explanations start the command from the Command Prompt...");>
        IfFileExists("%b\_Run.log",>
          !*CloseFile("%b\_Run.log",1);>
            Open("%b\_Run.log",1,1,-1);GotoCL(1,1);TrackCaret;*);`);

End;
